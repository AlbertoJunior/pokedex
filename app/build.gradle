plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
//    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp'
}

android {
    compileSdk = 34
    namespace = 'com.example.pokedex'

    defaultConfig {
        def commitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

        applicationId = 'aj.dev.pokedex'
        minSdk = 23
        targetSdk = 34
        versionCode = commitCount
        versionName = "1.$commitCount"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles(
                    getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            )
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
}

dependencies {
    def constraint_layout = "2.1.4"
    def material = '1.12.0'
    def appcompat = "1.7.0"
    def core_kt = '1.13.1'
    def ktx = '2.8.2'
    def ktx_nav = '2.7.7'
    def hilt = '2.51.1'
    def room = '2.6.1'
    def paging = "3.3.0"
    def lottie = '6.0.1'
    def gson = '2.11.0'
    def retrofit = "2.9.0"
    def okhttp = "5.0.0-alpha.5"
    def glide = '4.15.1'

    implementation "androidx.core:core-ktx:$core_kt"
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "com.google.android.material:material:$material"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$ktx"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$ktx"
    implementation "androidx.navigation:navigation-fragment-ktx:$ktx_nav"
    implementation "androidx.navigation:navigation-ui-ktx:$ktx_nav"

    // dependency injection
    implementation "com.google.dagger:hilt-android:$hilt"
    ksp "com.google.dagger:hilt-android-compiler:$hilt"

    // parser json
    implementation "com.google.code.gson:gson:$gson"

    // network
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.okhttp3:okhttp:$okhttp"

    // persistence
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    implementation "androidx.room:room-paging:$room"
    annotationProcessor "androidx.room:room-compiler:$room"
    ksp "androidx.room:room-compiler:$room"

    // image
    implementation "com.github.bumptech.glide:glide:$glide"
    ksp "com.github.bumptech.glide:compiler:$glide"

    // paging
    implementation "androidx.paging:paging-runtime-ktx:$paging"
    implementation "androidx.paging:paging-guava:$paging"

    //lottie
    implementation "com.airbnb.android:lottie:$lottie"

    // tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}